  use.miden::account
  use.miden::note
  use.miden::tx
  use.miden::contracts::wallets::basic->wallet
  
  const.ERR_WRONG_NUMBER_OF_INPUTS="Time-locked note expects exactly 3 note inputs"
  const.ERR_WRONG_BENEFICIARY="Only designated beneficiary can claim this inheritance"
  const.ERR_TOO_EARLY="Inheritance deadline has not passed yet"
  
  # Input Stack: []
  proc.add_note_assets_to_account
      push.0 exec.note::get_assets
      # => [num_of_assets, 0 = ptr, ...]

      # compute the pointer at which we should stop iterating
      mul.4 dup.1 add
      # => [end_ptr, ptr, ...]

      # pad the stack and move the pointer to the top
      padw movup.5
      # => [ptr, 0, 0, 0, 0, end_ptr, ...]

      # compute the loop latch
      dup dup.6 neq
      # => [latch, ptr, 0, 0, 0, 0, end_ptr, ...]

      while.true
        # => [ptr, 0, 0, 0, 0, end_ptr, ...]

        # save the pointer so that we can use it later
        dup movdn.5
        # => [ptr, 0, 0, 0, 0, ptr, end_ptr, ...]

        # load the asset
        mem_loadw
        # => [ASSET, ptr, end_ptr, ...]

        # pad the stack before call
        padw swapw padw padw swapdw
        # => [ASSET, pad(12), ptr, end_ptr, ...]

        # add asset to the account
        call.wallet::receive_asset
        # => [pad(16), ptr, end_ptr, ...]

        # clean the stack after call
        dropw dropw dropw
        # => [0, 0, 0, 0, ptr, end_ptr, ...]

        # increment the pointer and compare it to the end_ptr
        movup.4 add.4 dup dup.6 neq
        # => [latch, ptr+4, ASSET, end_ptr, ...]
      end

      # clear the stack
      drop dropw drop
  end
  
  # Input stack: [beneficiary_suffix, beneficiary_prefix, deadline]
  proc.verify_inheritance_claim
      
      # Push current account id to the stack
      exec.account::get_id
      # Stack: [account_suffix, account_prefix, beneficiary_suffix, beneficiary_prefix, deadline]

      # Verify if the current account id is the same as the beneficiary id
      exec.account::is_id_equal assert.err=ERR_WRONG_BENEFICIARY
      # Stack: [deadline]
  
      # Push current block number to the stack
      exec.tx::get_block_number
      # Stack: [block_number, deadline]

  
      # Verify the deadline has passed
      gte assert.err=ERR_TOO_EARLY
      # Stack: []
  
      # Execute release of funds
      exec.add_note_assets_to_account
  end
  
  begin
      # Push inputs to stack
      push.0 exec.note::get_inputs
      # Stack: [num_inputs, inputs_ptr]
      eq.3 assert.err=ERR_WRONG_NUMBER_OF_INPUTS
      # Stack: [inputs_ptr]
      padw movup.4 mem_loadw drop
      # Stack: [beneficiary_suffix, beneficiary_prefix, deadline]
  
      # Push sender id to stack
      exec.note::get_sender
      # Stack: [sender_suffix, sender_prefix, beneficiary_suffix, beneficiary_prefix, deadline]
  
      # Push current id to the stack
      exec.account::get_id
      # Stack: [account_suffix, account_prefix, sender_suffix, sender_prefix, beneficiary_suffix, beneficiary_prefix, deadline]
  
      # Do what it used to do before (sender address verification, if not the same => verify inheritance claim with loaded stack)
      exec.account::is_id_equal
      # Stack: [is_equal, beneficiary_suffix, beneficiary_prefix, deadline]
  
      if.true
        # Clear all stack elements and ensure clean state
        drop drop drop
        exec.add_note_assets_to_account
      else
        exec.verify_inheritance_claim
      end
  end 